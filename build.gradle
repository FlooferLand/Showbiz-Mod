plugins {
	id "fabric-loom" version "1.7.3"
	id "maven-publish"
}

// Settings and variables
version = project.mod_version + "-" + project.minecraft_version
group = project.maven_group
int minJavaVersion = Integer.parseInt(project.min_java_version)
archivesBaseName = archives_base_name

base {
	archivesName = project.archives_base_name
}

repositories {
	repositories {
		// Modrinth package repository
		exclusiveContent {
			forRepository {
				//noinspection ForeignDelegate - Modrinth-recommended
				maven {
					name = "Modrinth"
					url = "https://api.modrinth.com/maven"
				}
			}
			filter {
				includeGroup "maven.modrinth"
			}
		}
		
		// GeckoLib - animation & model loading
		maven {
			name = "GeckoLib"
			url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
			content {
				includeGroup("software.bernie.geckolib")
			}
		}

		// Cloth Config
		maven { url "https://maven.shedaniel.me/" }
		maven { url "https://maven.terraformersmc.com/releases/" }
	}
}

// loom {
// 	accessWidenerPath = file("src/main/resources/showbiz.accesswidener")
// }

fabricApi {
	configureDataGeneration()
}

// TODO: Make sure isDatagen is only true when running via
def isServer = ((!project.hasProperty("fabric.dli.env")) || project.property("fabric.dli.env") == "server")
def isDatagen = project.hasProperty("fabric-api.datagen") && project.property("fabric.dli.env") == "server"
dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"

	// Mod dependencies
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
	modImplementation "software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}"
	modCompileOnly "maven.modrinth:modmenu:${mod_menu_version}"
	
	// OGG Vorbis reading library for data generation.
	// Using this library because Mojang/Fabric"s code prevents usage on server-side for some stupid reason
	//if (isDatagen) {
	implementation "ws.schild:jave-all-deps:${jave_version}"
	//}
}

// Injecting gradle.properties into other files
processResources {
	exclude("**/*.bbmodel", "**/*.lnk")

	Map<String, Object> properties = [
			"version": version,
			"minecraft_version": minecraft_version,
			"loader_version": loader_version,
			"fabric_version": fabric_version,
			"min_java_version": minJavaVersion.toString(),

			// Mod dependencies
			"geckolib_version": geckolib_version,
	]
	properties.forEach((k, v) -> inputs.property(k, v))

	filesMatching("fabric.mod.json") {
		expand properties
	}
	filesMatching(archivesBaseName + ".mixins.json") {
		expand "archivesBaseName": archivesBaseName
	}

	duplicatesStrategy = DuplicatesStrategy.WARN
}

// Java version
tasks.withType(JavaCompile).configureEach {
	it.options.release = minJavaVersion
}
java {
	withSourcesJar()
	sourceCompatibility = minJavaVersion - 1
	targetCompatibility = minJavaVersion - 1
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}